apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    PUBLISH_GROUP_ID = "io.github.huangwenge"
    PUBLISH_VERSION = "1.0.0"
}

ext["signing.keyId"] = '' //签名的密钥后8位
ext["signing.password"] = ''  //签名设置的密码
ext["signing.secretKeyRingFile"] = '' //生成的secring.gpg文件目录
ext["ossrhUsername"] = ''  //sonatype用户名
ext["ossrhPassword"] = ''  //sonatype密码

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        release(MavenPublication) {
            from components.java
            groupId PUBLISH_GROUP_ID
            artifactId project.name
            version PUBLISH_VERSION
            artifact javadocJar
            artifact kotlinSourcesJar

            pom {
                name = project.name
                description = 'android plugin registry'
                // If your project has a dedicated site, use its URL here
                url = 'https://github.com/huangwenge/PluginRegistry'
                licenses {
                    license {
                        //协议类型，一般默认Apache License2.0的话不用改：
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = ossrhUsername
                        name = "huangwenge"
                        email = "847880529@qq.com"
                    }
                }
                // Version control info, if you're using GitHub, follow the format as seen here
                scm {
                    //修改成你的Git地址：
                    connection = 'git@github.com:huangwenge/PluginRegistry.git'
                    developerConnection = 'git@github.com:huangwenge/PluginRegistry.git'
                    //分支地址：
                    url = 'https://github.com/huangwenge/PluginRegistry.git/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            name = "MavenCentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = PUBLISH_VERSION.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}